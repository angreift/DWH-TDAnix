-- =============================================
-- Author:		kma1860
-- Create date: 27/10/2022
-- Description:	Возвращает срез остатков на выбранную дату
-- =============================================
CREATE FUNCTION [td].[f_Срез_остатков_на_дату]
(	
	@Дата date 
)
RETURNS TABLE 
AS
RETURN 
(
	select
		@Дата дата, Код_магазина, Код_склада_в_магазине, Код_товара, sum(Сумма) Сумма, sum(Сумма_закупа) Сумма_закупа, sum(Сумма_закупа_со_склада) Сумма_закупа_со_склада, sum(Остаток) Остаток
	from (
		select
			ib_f.Код_магазина, ib_f.Код_склада_в_магазине, ib_f.Код_товара, ib.Сумма, ib.Сумма_закупа, ib.Сумма_закупа_со_склада, ib.Остаток
		from
			td.t_fact_Начальные_остатки ib
		right join (
			select
				max(ib_f.Дата) Дата,
				ib_f.Код_Товара,
				ib_f.Код_магазина,
				ib_f.Код_склада_в_магазине
			from
				td.t_fact_Начальные_остатки ib_f
			where
				ib_f.Дата = dateadd(day, -1, dateFromParts(year(@Дата), month(@Дата), 1))
			group by 
				ib_f.Код_товара, ib_f.Код_магазина, ib_f.Код_склада_в_магазине
		) ib_f on ib.Дата = ib_f.Дата and ib.Код_товара = ib_f.Код_товара and ib.Код_магазина = ib_f.Код_магазина and ib.Код_склада_в_магазине = ib_f.Код_склада_в_магазине
		union all
		select
			Код_магазина, Склад_в_магазине, Код_товара, Сумма, СуммаЗакупа, СуммаЗакупаСоСклада, Количество
		from
			td.t_fact_Товародвижение
		where
			Дата <= @Дата and Дата > dateadd(day, -1, dateFromParts(year(@Дата), month(@Дата), 1))
	) о
	group by Код_магазина, Код_склада_в_магазине, Код_товара
)
GO



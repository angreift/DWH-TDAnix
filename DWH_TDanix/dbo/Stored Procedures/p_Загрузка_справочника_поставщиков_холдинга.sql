-- =============================================
-- Author:		kma1860
-- Create date: 31/01/2023
-- Description:	Загрузка справочника "Поставщики поставщиков" из serv-term
-- =============================================
CREATE PROCEDURE [dbo].[p_Загрузка_справочника_поставщиков_холдинга]
AS
BEGIN

	SET NOCOUNT ON;

	-- 1. Создание временной таблицы
	drop table if exists #dwh_temp_поставщики_холдинга;

	create table #dwh_temp_поставщики_холдинга (
		[Код_поставщика_холдинга] int not null,
		[Наименование] nvarchar(40) not null,
		[ИНН] nvarchar(20) null,
		[КПП] nvarchar(19) null
	);

	-- 2. Загрузка из serv-term
	insert into #dwh_temp_поставщики_холдинга (Код_поставщика_холдинга, Наименование, ИНН, КПП)
	select
		Поставщики.[CODE] as [Код_поставщика_холдинга],
		Поставщики.[DESCR] as [Наименование],
		Поставщики.[SP44] as [ИНН],
		Поставщики.[SP2160] as [КПП]
	from [Rozn].[rozn].[dbo].[SC36] as Поставщики
	where Поставщики.[ISFOLDER] = 2 and Поставщики.[PARENTID] = '  12EE   '; -- Внтренний ид группы "Поставщики поставщиков"

	-- 3. Слияние таблиц
	merge
		[dbo].t_dim_Поставщики_холдинга
	using
		#dwh_temp_поставщики_холдинга
	on
		[dbo].t_dim_Поставщики_холдинга.[Код_поставщика_холдинга] = #dwh_temp_поставщики_холдинга.[Код_поставщика_холдинга]
	when matched 
		then update 
			set
				[dbo].t_dim_Поставщики_холдинга.Наименование = #dwh_temp_поставщики_холдинга.Наименование,
				[dbo].t_dim_Поставщики_холдинга.ИНН = #dwh_temp_поставщики_холдинга.ИНН,
				[dbo].t_dim_Поставщики_холдинга.КПП = #dwh_temp_поставщики_холдинга.КПП
	when not matched
		then insert
			values(
				#dwh_temp_поставщики_холдинга.Код_поставщика_холдинга,
				#dwh_temp_поставщики_холдинга.Наименование,
				#dwh_temp_поставщики_холдинга.ИНН,
				#dwh_temp_поставщики_холдинга.КПП
			);
END;
GO


